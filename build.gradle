plugins {
    id "de.undercouch.download" version "5.3.1"
    id "com.diffplug.p2.asmaven" version "3.26.1" apply false
    id "biz.aQute.bnd.builder" version "5.1.2" apply false
    id "org.standardout.bnd-platform" version "1.7.0" apply false
}

ext {
    haleVersion = project.findProperty("dev") ? '5.1.0.SNAPSHOT' : project.findProperty("haleVersion") ?: '5.0.0'
    configUtilVersion = '2.0.1'
    ldproxyCfgVersion = '3.4.0-cfglib-SNAPSHOT'
    p2Group = 'hale-platform'
    os = project.findProperty("os") ?: 'linux'
    dev = project.findProperty("dev") ?: false
    haleDir = file("$buildDir/hale")
    haleContents = os == "macos" ? file("$buildDir/hale/hale studio.app/Contents/Eclipse/") : file("$buildDir/hale/")
    haleRepo = os == "macos" ? file("$haleContents/plugins/") : file("$haleContents/plugins/")
    haleReleaseName = os == 'linux'
        ? "hale-studio-${haleVersion}-linux.gtk.x86_64.tar.gz"
        : os == 'windows'
            ? "hale-studio-${haleVersion}-win32.win32.x86_64.zip"
            : os == 'macos'
                ? "hale-studio-${haleVersion}-macosx.cocoa.x86_64.tar.gz"
                : null
}

allprojects {
    group = 'de.interactive_instruments'

    version = '3.0.0'

    repositories {
        mavenCentral()
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
    }
}

subprojects {
    ext {
        isPlugin = projectDir.toPath().parent.fileName.toString() == 'plugins'
    }

    if (isPlugin) {
        apply plugin: 'java'
        apply plugin: 'biz.aQute.bnd.builder'

        java {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        evaluationDependsOn(':hale')

        repositories {
            ivy {
                name 'hale-platform2'
                metadataSources {
                    artifact()
                }
                artifactPattern "$haleRepo/[module]_[revision].jar"
            }
        }

        afterEvaluate {
            jar.manifest.attributes(
                    '-exportcontents': 'de.ii.*',
                    '-removeheaders': 'Bnd-LastModified,Created-By,Tool,Require-Capability',
                    'Import-Package': '!javax.annotation.*,!de.ii.xtraserver.hale.io.*,*',
                    'Bundle-Name': description,
                    'Bundle-SymbolicName': "${group}.${name.replaceAll('-','.')};singleton:=true",
                    'Bundle-Vendor': 'interactive-instruments GmbH',
                    'Bundle-License': "https://www.gnu.org/licenses/lgpl-3.0"
            )
        }
    }
}

apply from: './mvn2osgi.gradle'

apply from: './hale.gradle'
